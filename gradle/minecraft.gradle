ext.api = common.project('SpongeAPI')

apply from: api.file('gradle/java.gradle')

evaluationDependsOn api.path

minecraftPlugin.replacer.putReplacement('SRG_DIR', "$DIR_MCP_MAPPINGS/srgs")

// Mixins
ext {
    mixinSrg = new File(project.buildDir, 'tmp/mixins/mixins.srg')
    mixinRefMap = new File(project.buildDir, "tmp/mixins/mixins.${implementation.toLowerCase()}.refmap.json")
}

minecraft {
    if (!project.ext.has('forgeBuild')) {
        version = '1.8'
    }

    mappings = 'snapshot_20150301'
    runDir = 'run'
}

// Compile options
compileJava {
    options.compilerArgs += [
            '-Xlint:-processing',
            "-AoutSrgFile=${project.mixinSrg.canonicalPath}",
            "-AoutRefMapFile=${project.mixinRefMap.canonicalPath}",
            "-AreobfSrgFile=${project.file('build/srgs/mcp-srg.srg').canonicalPath}"
    ]
}

// Copy the current srgs to the build dir so the user doesn't have to go hunting for them
task copySrgs(type: Copy, dependsOn: 'genSrgs') {
    from minecraftPlugin.delayedFile('{SRG_DIR}')

    include '**/*.srg'
    into 'build/srgs'
}

setupDecompWorkspace.dependsOn copySrgs
setupDevWorkspace.dependsOn copySrgs
compileJava.dependsOn copySrgs

deobfMcMCP { failOnAtError = false }
deobfMcSRG { failOnAtError = false }

plugins.withId('net.minecraftforge.gradle.tweaker-server') {
    // Scan for access transformers
    sourceSets.main.resources.files.each {
        if (it.name.endsWith('_at.cfg')) {
            logger.lifecycle("Found AccessTransformer in main resources: $it.name")
            minecraft.at it
        }
    }

    afterEvaluate {
        reobfJar.primarySrg = minecraftPlugin.delayedFile('{SRG_DIR}/mcp-srg.srg')
    }
}

// Configure reobfuscation for mixins
reobfJar.doFirst {
    if (project.mixinSrg.exists()) {
        addSecondarySrgFile project.mixinSrg
    }
}

// Set manifest entries
jar {
    from project.mixinRefMap

    manifest {
        attributes(
                'Specification-Name': api.name,
                'Specification-Version': api.version,
                'Specification-Vendor': api.url
        )
    }
}

// Use checkstyle suppressions from Common
checkstyle {
    configProperties['suppressions'] = common.file('checkstyle-suppressions.xml')
}

license {
    ext.name = 'Sponge'
}
