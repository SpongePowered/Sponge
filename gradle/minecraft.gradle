// Shared Gradle configuration for the Sponge ForgeGradle projects

ext.api = common.project('SpongeAPI')

// Finish configuring SpongeAPI first
evaluationDependsOn api.path

// Apply shared Gradle configuration in SpongeAPI
apply from: api.file('gradle/sponge.gradle')

// Apply ForgeGradle plugin
apply plugin: forgeGradlePlugin

// Apply MixinGradle plugin to project
apply plugin: 'org.spongepowered.mixin'

minecraft {
    version = minecraftVersion
    mappings = 'snapshot_20160822'

    runDir = 'run'

    // We don't use the obfuscated dev jar currently
    makeObfSourceJar = false
}

// Mixins
sourceSets {
    main {
        refMap = "mixins.${implementation.toLowerCase()}.refmap.json"
    }
}

// Java 6 source set for compatibility checking
sourceSets {
    java6 {
        compileClasspath += main.compileClasspath + main.output
    }
}

// Compile options
compileJava {
    options.compilerArgs += '-Xlint:-processing'
}

compileJava6Java {
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
}

// ForgeGradle 2 fails to apply transformer rules for classes it doesn't know of by default
// We use the access transformer to make the catalogs in the API accessible
deobfMcMCP { failOnAtError = false }
deobfMcSRG { failOnAtError = false }

// The access transformers we use
ext.ats = ['common_at.cfg']

// Set manifest entries
jar {
    // Include Java 6 classes
    from sourceSets.java6.output

    manifest {
        attributes(
                'Specification-Title': api.name,
                'Specification-Version': api.version,
                'Specification-Vendor': api.url
        )
    }

    afterEvaluate {
        manifest {
            attributes('FMLAT': project.ats.join(' '))
        }
    }
}

// Don't upload temp jar to Maven repository
configurations.archives.artifacts.removeAll { it.archiveTask.is jar }

task devJar(type: Jar) {
    classifier = 'dev'
    duplicatesStrategy = 'exclude' // Exclude duplicate LICENSE.txt file

    // Inherit manifest from jar
    manifest.from jar.manifest

    // Include classes
    from sourceSets.main.output
    from sourceSets.java6.output

    // Include API
    from api.sourceSets.main.output
    from api.sourceSets.ap.output
}

artifacts {
    // Upload dev jar to Maven repository
    archives devJar
}

task sourceJar(type: Jar, overwrite: true) {
    classifier = 'sources'

    // Include sources
    from sourceSets.main.allSource
    from sourceSets.java6.allSource

    // Include SpongeAPI sources in source jar
    from api.sourceSets.main.allSource
    from api.sourceSets.ap.allSource
}

artifacts {
    // Upload source jar to Maven repository
    archives sourceJar
}

// Include SpongeAPI/SpongeCommon dependencies in our POM
// Note: This is ugly, just keep scrolling
uploadArchives.repositories.mavenDeployer.pom.whenConfigured {
    dependencies.removeAll { it.artifactId == shadedDevProject.archivesBaseName }
    def deps = new HashSet(dependencies.collect { it.groupId + ':' + it.artifactId })
    dependencies.addAll(shadedDevProject.uploadArchives.repositories.mavenDeployer.pom.effectivePom.dependencies.findAll {
        !deps.contains(it.groupId + ':' + it.artifactId) // Don't add the same dependency several times to the POM
    })
}

configure([jar, devJar]) {
    // Append API revision to the SpongeAPI version
    dependsOn common.resolveApiRevision
}

common.tasks.resolveApiRevision << {
    jar.manifest.attributes('Specification-Version': common.apiVersion)
}

// Code style configuration
sortAccessTransformers {
    add sourceSets.main
}

license {
    ext.name = 'Sponge'
}

// Use checkstyle suppressions from Common
checkstyle {
    configProperties['suppressions'] = common.file('checkstyle-suppressions.xml')
}
